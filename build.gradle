import com.amazonaws.AmazonClientException
import com.amazonaws.auth.EnvironmentVariableCredentialsProvider
import com.amazonaws.auth.profile.*
import com.amazonaws.services.s3.*
import com.amazonaws.services.s3.model.*

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.amazonaws:aws-java-sdk-core:1.11.25'
        classpath 'com.amazonaws:aws-java-sdk-s3:1.11.25'
    }
}

plugins {
    id 'java'
    id 'groovy'
    id 'jacoco'
    id 'ivy-publish'
    id 'codenarc'
}

repositories {
    mavenCentral()
    ivy {
        artifactPattern 'http://sqreen-ci-java.s3-website-us-east-1.amazonaws.com/jars/[organisation]/[artifact]-[revision](-[classifier]).[ext]'
        ivyPattern 'http://sqreen-ci-java.s3-website-us-east-1.amazonaws.com/jars/[organisation]/[module]-[revision].xml'
    }
}

group 'io.sqreen'
version '9.0.2'

sourceCompatibility = 1.8
targetCompatibility = 1.8

def WINDOWS = org.gradle.internal.os.OperatingSystem.current().windows
def MACOS = org.gradle.internal.os.OperatingSystem.current().macOsX
def cmakeBuildType = hasProperty('debug') ? 'Debug' : 'RelWithDebInfo'
def cmakeBuildDir = "$projectDir/build-cmake/$cmakeBuildType"

task cmakeNativeLib(type: Exec) {
    outputs.upToDateWhen { false }
    description = 'Runs cmake for libddwaf JNI bindings'

    doFirst {
        file(cmakeBuildDir).mkdirs()
    }

    inputs.file 'CMakeLists.txt'
    inputs.file 'deps/CMakeLists.txt'
    outputs.dir cmakeBuildDir

    logging.captureStandardError  LogLevel.ERROR
    logging.captureStandardOutput LogLevel.INFO

    def cmakeOpts = []
    if (project.hasProperty('withASAN')) {
        cmakeOpts += [
            '-DCMAKE_C_COMPILER=clang',
            '-DCMAKE_CXX_COMPILER=clang++',
            '-DCMAKE_C_FLAGS=-fsanitize=address -fsanitize=undefined -fsanitize=leak --coverage',
            '-DCMAKE_SHARED_LINKER_FLAGS=-fsanitize=address -fsanitize=undefined -fsanitize=leak -lubsan -lasan --coverage',
        ]
        assert !WINDOWS, 'Address sanitizer not supported on Windows'
    }
    if (project.hasProperty('debug')) {
        cmakeOpts += [
            '-DCMAKE_C_FLAGS=--coverage',
            '-DCMAKE_SHARED_LINKER_FLAGS=--coverage',
        ]
    }
    if (project.hasProperty('macArch')) {
        cmakeOpts += ["-DCMAKE_OSX_ARCHITECTURES=${project.macArch}"]
    }

    commandLine 'cmake', "-DCMAKE_BUILD_TYPE=$cmakeBuildType", *cmakeOpts, '-S', projectDir, '-B', cmakeBuildDir
    workingDir cmakeBuildDir
}

task buildNativeLib(type: Exec) {
    description = 'Builds libddwaf JNI bindings'
    group = 'test'

    inputs.dir 'src/main/c'
    dependsOn cmakeNativeLib
    if (WINDOWS) {
        outputs.file "$cmakeBuildDir/sqreen_jni.dll"
    } else if (MACOS) {
        outputs.file "$cmakeBuildDir/libsqreen_jni.dylib"
    } else {
        outputs.file "$cmakeBuildDir/libsqreen_jni.so"
    }

    logging.captureStandardError  LogLevel.ERROR
    logging.captureStandardOutput LogLevel.INFO

    commandLine 'cmake', '--build', '.', '--parallel', '--verbose', '--config', cmakeBuildType, '--target', 'sqreen_jni'
    workingDir cmakeBuildDir
}

task copyLibddwaf(type: Copy) {
    description = 'Copies libddwaf to the resources directory'
    group = 'build'

    def libName = WINDOWS ? 'ddwaf.dll' : MACOS ? 'libddwaf.dylib' : 'libddwaf.so'
    from "$cmakeBuildDir"
    into sourceSets.main.output.resourcesDir
    include libName
    rename libName, 'libddwaf.so'
}

task prepare

jacocoTestReport {
    executionData fileTree(project.buildDir.absolutePath).include("jacoco/*.exec")
    reports {
        html.required = true
        xml.required = true
        csv.required = false
    }
    mustRunAfter 'testgc'
    mustRunAfter 'testNoByteBuffers'
}

sourceSets {
    testGC {
        java.srcDir "src/test_gc/java"
        groovy.srcDir "src/test_gc/groovy"
        resources.srcDir "src/test_gc/resources"
    }
}
configurations {
    testGCCompileClasspath.extendsFrom(testCompileClasspath)
    testGCRuntimeClasspath.extendsFrom(testRuntimeClasspath)
}

def SLF4J_VERSION = '1.7.30'
dependencies {
    implementation group: 'org.slf4j', name: 'slf4j-api', version: SLF4J_VERSION

    testRuntimeOnly group: 'org.slf4j', name: 'slf4j-simple', version: SLF4J_VERSION
    testImplementation group: 'org.hamcrest', name: 'hamcrest', version: '2.2'
    testImplementation group: 'org.apache.groovy', name: 'groovy-all', version: '4.0.18'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.9.2'

    testGCRuntimeOnly files(sourceSets.main.output)
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

def nativeLibsDir = "$projectDir/native_libs"

task checkForNativeLibs {
    doLast {
        def tree = fileTree(dir: nativeLibsDir, include: ['**/*.so', '**/*.dylib', '**/*.dll'])
        if (tree.empty) {
            throw new GradleException("Could find no libraries in $tree")
        }
    }
}

task packageDebugSymbols(type: Zip) {
    archiveClassifier = 'dbgsym'

    from nativeLibsDir, {
        include '**/*.pdb'
        include '**/*.debug'
        include '**/*.dwarf'
    }

    dependsOn checkForNativeLibs
}

def creds = null
try {
    //looks in AWS_ACCESS_KEY_ID (or AWS_ACCESS_KEY) and AWS_SECRET_KEY
    // (or AWS_SECRET_ACCESS_KEY) environment variables.
    creds = new EnvironmentVariableCredentialsProvider().credentials
} catch (AmazonClientException e) {
    logger.warn 'No AWS credentials in env variables'
    try {
        // creds can be in env variables or ~/.aws/{credentials,config}
        creds = new ProfileCredentialsProvider('mfa').credentials
    } catch (IllegalArgumentException ee) {
        logger.warn 'No AWS credentials in ~/.aws/credentials'
    } catch (NullPointerException npe) {
        logger.warn 'No AWS credentials in ~/.aws/credentials'
    }
}

publishing {
    repositories {
        ivy {
            artifactPattern 's3://sqreen-ci-java/jars/[organisation]/[artifact]-[revision](-[classifier]).[ext]'
            ivyPattern 's3://sqreen-ci-java/jars/[organisation]/[module]-[revision].xml'

            if (creds) {
                credentials(AwsCredentials) {
                    accessKey = creds.AWSAccessKeyId
                    secretKey = creds.AWSSecretKey
                    if (creds.hasProperty('sessionToken')) {
                        sessionToken = creds.sessionToken
                    }
                }
            }
        }
    }
    publications {
        ivy(IvyPublication) {
            from components.java
            artifact(sourcesJar) {
                type = 'sources'
                conf = 'compile'
            }
            artifact(packageDebugSymbols) {
                classifier = 'dbgsym'
                extension = 'zip'
            }
            descriptor.description {
                text = description
            }
        }
    }
}

task fixS3Permissions {
    doLast {
        def bucket = 'sqreen-ci-java'
        def s3 = new AmazonS3Client(creds)
        ObjectListing listing = s3.listObjects(bucket,
                "jars/io.sqreen/libsqreen-${project.version}")
        for (S3ObjectSummary summary: listing.objectSummaries) {
            logger.info "Setting ACL for ${summary.key}"
            s3.setObjectAcl(bucket, summary.key, CannedAccessControlList.PublicRead)
        }
    }
    publish.finalizedBy it
}

task copyNativeLibs(type: Copy) {
    def existsLib = { filename ->
        def fileObj = file("$nativeLibsDir/${filename}")
        if (!fileObj.exists()) {
            logger.warn("Could not find ${fileObj.absolutePath}")
            false
        } else {
            true
        }
    }
    def directories = [['linux/x86_64/glibc/libsqreen_jni.so', 'linux/x86_64/libddwaf.so'],
                       ['linux/x86_64/musl/libsqreen_jni.so', 'linux/x86_64/libddwaf.so'],
                       ['linux/aarch64/glibc/libsqreen_jni.so', 'linux/aarch64/libddwaf.so'],
                       ['linux/aarch64/musl/libsqreen_jni.so', 'linux/aarch64/libddwaf.so'],
                       ['macos/x86_64/libsqreen_jni.dylib', 'macos/x86_64/libddwaf.dylib'],
                       ['macos/aarch64/libsqreen_jni.dylib', 'macos/aarch64/libddwaf.dylib'],
                       ['windows/x86_64/sqreen_jni.dll']]

    doFirst {
        if (!directories.any { files -> files.every(existsLib) }) {
            throw new GradleException("Could not find any native lib")
        }
    }

    from nativeLibsDir, {
        include '**/*.so'
        include '**/*.dylib'
        include '**/*.dll'
    }
    into "${sourceSets.main.output.resourcesDir}/native_libs/"

    dependsOn checkForNativeLibs
}

tasks.withType(JavaCompile).configureEach {
    mustRunAfter copyNativeLibs
}
jar.dependsOn copyNativeLibs

task testgc(type: Test) {
    description = 'Tests that the library can be unloaded'
    group = 'verification'
    useJUnit()

    testClassesDirs = sourceSets.testGC.output.classesDirs
    classpath = sourceSets.testGC.runtimeClasspath
}
check.dependsOn testgc

task testNoByteBuffers(type: Test) {
    group = 'verification'

    jvmArgs += ['-DPOWERWAF_ENABLE_BYTE_BUFFERS=0']
}
check.dependsOn testNoByteBuffers

tasks.withType(Test).each {
    if (System.getenv('TEST_EXECUTABLE')) {
        it.executable System.getenv('TEST_EXECUTABLE')
    }

    it.jvmArgs += ['-Xcheck:jni', '-Dorg.slf4j.simpleLogger.defaultLogLevel=DEBUG', '-DPOWERWAF_EXIT_ON_LEAK=true']
    if (project.hasProperty('useReleaseBinaries')) {
        it.jvmArgs += ['-DuseReleaseBinaries=true']
        it.dependsOn copyNativeLibs
    } else {
        it.jvmArgs += ["-Djava.library.path=$cmakeBuildDir"]
        it.dependsOn buildNativeLib
    }

    it.outputs.upToDateWhen { false }
}

// the headers are incorrect wrt inner classes
// see https://bugs.openjdk.java.net/browse/JDK-8145897
task generateJniHeaders(type: JavaCompile) {
    classpath = sourceSets.main.compileClasspath
    destinationDirectory = file("${buildDir}/generated/jni")
    source = sourceSets.main.java
    options.compilerArgs += [
            '-h', file("${buildDir}/generated/jni"),
            '-d', file("${buildDir}/generated/jni-tmp")
    ]
    // options.verbose = true
    doFirst {
        ['jni', 'jni-tmp'].each { mkdir "${buildDir}/generated/$it" }
    }
    doLast {
        delete file("${buildDir}/generated/jni-tmp")
    }
}

codenarc {
    configFile = file("${project.projectDir}/gradle/codenarc.groovy")
}
codenarcTest {
    compilationClasspath = codenarcMain.compilationClasspath +
            sourceSets.test.compileClasspath + sourceSets.test.output
}

// vim: set et ts=4 sw=4 list:
