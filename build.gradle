import com.amazonaws.AmazonClientException
import com.amazonaws.auth.EnvironmentVariableCredentialsProvider
import com.amazonaws.auth.profile.*

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.amazonaws:aws-java-sdk-core:1.11.5'
    }
}

plugins {
    id 'java'
    id 'groovy'
    id 'jacoco'
    id 'ivy-publish'
}

repositories {
    jcenter()
    ivy {
        artifactPattern 'http://sqreen-ci-java.s3-website-us-east-1.amazonaws.com/jars/[organisation]/[artifact]-[revision](-[classifier]).[ext]'
        ivyPattern 'http://sqreen-ci-java.s3-website-us-east-1.amazonaws.com/jars/[organisation]/[module]-[revision].xml'
    }
}

group 'io.sqreen'
version '0.6.0'

sourceCompatibility = 1.6
targetCompatibility = 1.6

def WINDOWS = org.gradle.internal.os.OperatingSystem.current().windows

task cmakeNativeLibDebug(type: Exec) {
    description = 'Runs cmake in a debug configuration'

    def pwafConfDir = project.hasProperty('powerwafConfig') ?
            project.powerwafConfig :
            "$projectDir/PowerWAF/Debug/out/usr/local/share/cmake/powerwaf"

    doFirst {
        def f = file("$pwafConfDir/powerwafConfig.cmake")
        if (!f.exists()) {
            logger.warn("The file $f does not exist. PowerWAF debug must be installed globally. " +
                    "Run with -PpowerwafConfig=/path/to/dir/of/powerwafConfig-debug.cmake to override")
        }

        file("$buildDir/Debug").mkdirs()
    }

    inputs.file 'CMakeLists.txt'
    outputs.dir "$buildDir/Debug/"

    logging.captureStandardError  LogLevel.ERROR
    logging.captureStandardOutput LogLevel.INFO

    def cmakeOpts
    if (project.hasProperty('withASAN')) {
        cmakeOpts = [
            '-DCMAKE_C_FLAGS=-fsanitize=address -fsanitize=leak --coverage',
            '-DCMAKE_SHARED_LINKER_FLAGS=-fsanitize=address -fsanitize=leak --coverage',
        ]
        if (WINDOWS) {
            logger.warn('Address sanitizer not supported on Windows')
        }
    } else {
        cmakeOpts = [
            '-DCMAKE_C_FLAGS=--coverage',
            '-DCMAKE_SHARED_LINKER_FLAGS=--coverage',
        ]
    }

    if (WINDOWS) {
        commandLine 'cmake', "-DCMAKE_PREFIX_PATH=$pwafConfDir", projectDir
    } else {
        commandLine('cmake', '-DCMAKE_BUILD_TYPE=Debug', "-DCMAKE_PREFIX_PATH=$pwafConfDir",
                             *cmakeOpts, projectDir)
    }

    workingDir "$buildDir/Debug"
}

task buildNativeLibDebug(type: Exec) {
    description = 'Builds the native JNI lib in a debug configuration'
    group = 'test'

    inputs.dir 'src/main/c'
    if (WINDOWS) {
        outputs.file "$buildDir/Debug/Debug/powerwaf_jni.dll"
    } else {
        outputs.file "$buildDir/Debug/libpowerwaf_jni.so"
    }

    logging.captureStandardError  LogLevel.ERROR
    logging.captureStandardOutput LogLevel.INFO

    if (WINDOWS) {
        commandLine 'cmake', '--build', '.', '--target', 'powerwaf_jni',
                             '-j', '--verbose', '--config', 'Debug'
    } else {
        commandLine 'make', '-j', 'powerwaf_jni', 'VERBOSE=1'
    }
    workingDir "$buildDir/Debug"

    dependsOn cmakeNativeLibDebug
}

jacocoTestReport {
    reports {
        html.enabled true
        xml.enabled true
        csv.enabled false
    }
}

dependencies {
    compile group: 'io.sqreen', name: 'logging', version: '2.1'
    compile group: 'com.google.guava', name: 'guava', version: '20.0', { // last version for java6
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
        exclude group: 'com.google.errorprone',    module: 'error_prone_annotations'
        exclude group: 'com.google.j2objc',        module: 'j2objc-annotations'
        exclude group: 'org.codehaus.mojo',        module: 'animal-sniffer-annotations'
    }

    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
    testCompile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.4.17'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

def nativeLibsDir = "$projectDir/native_libs"

task checkForNativeLibs {
    doLast {
        def tree = fileTree(dir: nativeLibsDir, include: ['**/*.so', '**/*.dylib', '**/*.dll'])
        if (tree.empty) {
            throw new GradleException("Could find no libraries in $tree")
        }
    }
}

task packageDebugSymbols(type: Zip) {
    archiveClassifier = 'dbgsym'

    from nativeLibsDir, {
        include '**/*.pdb'
        include '**/*.debug'
        include '**/*.dwarf'
    }

    dependsOn checkForNativeLibs
}

publishing {
    repositories {
        ivy {
            artifactPattern 's3://sqreen-ci-java/jars/[organisation]/[artifact]-[revision](-[classifier]).[ext]'
            ivyPattern 's3://sqreen-ci-java/jars/[organisation]/[module]-[revision].xml'

            def creds = null
            try {
                //looks in AWS_ACCESS_KEY_ID (or AWS_ACCESS_KEY) and AWS_SECRET_KEY
                // (or AWS_SECRET_ACCESS_KEY) environment variables.
                creds = new EnvironmentVariableCredentialsProvider().credentials
            } catch (AmazonClientException e) {
                logger.warn 'No AWS credentials in env variables'
                try {
                    // creds can be in env variables or ~/.aws/{credentials,config}
                    creds = new ProfileCredentialsProvider().credentials
                } catch (IllegalArgumentException ee) {
                    logger.warn 'No AWS credentials in ~/.aws/credentials'
                }
            }

            if (creds) {
                credentials(AwsCredentials) {
                    accessKey = creds.AWSAccessKeyId
                    secretKey = creds.AWSSecretKey
                }
            }
        }
    }
    publications {
        ivy(IvyPublication) {
            from components.java
            artifact(sourcesJar) {
                type = 'sources'
                conf = 'compile'
            }
            artifact(packageDebugSymbols) {
                classifier = 'dbgsym'
                extension = 'zip'
            }
            descriptor.description {
                text = description
            }
        }
    }
}

task copyNativeLibs(type: Copy) {
    def existsLib = { filename ->
        def fileObj = file("$nativeLibsDir/${filename}")
        if (!fileObj.exists()) {
            logger.warn("Could not find ${fileObj.absolutePath}")
            false
        } else {
            true
        }
    }
    def directories = [['linux_64_glibc/libpowerwaf_jni.so', 'linux_64_glibc/libSqreen.so'],
                       ['linux_64_musl/libpowerwaf_jni.so', 'linux_64_musl/libSqreen.so'],
                       ['solaris_64/libpowerwaf_jni.so', 'solaris_64/libSqreen.so'],
                       ['osx_64/libpowerwaf_jni.dylib', 'osx_64/libSqreen.dylib.dwarf'],
                       ['windows_64/powerwaf_jni.dll']]


    doFirst {
        if (!directories.any { files -> files.every(existsLib) }) {
            throw new GradleException("Could not find any native lib")
        }
    }

    from nativeLibsDir, {
        include '**/*.so'
        include '**/*.dylib'
        include '**/*.dll'
    }
    into "${sourceSets.main.output.resourcesDir}/native_libs/"

    dependsOn checkForNativeLibs
}

jar.dependsOn(copyNativeLibs)

sourceSets {
    testGC {
        java.srcDir "src/test_gc/java"
        groovy.srcDir "src/test_gc/groovy"
        resources.srcDir "src/test_gc/resources"

        compileClasspath += sourceSets.main.output + sourceSets.test.output
        runtimeClasspath += sourceSets.main.output + sourceSets.test.output
    }
}
configurations {
    testGCCompile.extendsFrom(testCompile)
    testGCRuntime.extendsFrom(testRuntime)
}

task testgc(type: Test) {
    description = 'Tests that the library can be unloaded'
    group = 'verification'
    useJUnit()

    testClassesDirs = sourceSets.testGC.output.classesDirs
    classpath = sourceSets.testGC.runtimeClasspath

    outputs.upToDateWhen { false }
}
check.dependsOn testgc

tasks.withType(Test).each {
    if (project.hasProperty('useReleaseBinaries')) {
        it.jvmArgs(['-DuseReleaseBinaries=true'])
        it.dependsOn copyNativeLibs
    } else {
        def javaLibPath = WINDOWS ? "$buildDir\\Debug\\Debug" : "$buildDir/Debug"
        it.jvmArgs(["-Djava.library.path=$javaLibPath"])
        it.dependsOn buildNativeLibDebug
    }

    it.outputs.upToDateWhen { false }
}

// vim: set et ts=4 sw=4 list:
